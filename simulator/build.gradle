plugins {
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'org.adripc64.guidesmiths.challenges.martianrobots'
version "${version}"

sourceCompatibility = '11'

// Required by the 'shadowJar' task
project.setProperty("mainClassName", "org.adripc64.guidesmiths.challenges.martianrobots.simulator.MartianRobotsSimulator")

application {
    mainClass = 'org.adripc64.guidesmiths.challenges.martianrobots.simulator.MartianRobotsSimulator'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':domain')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

run {
    standardInput = System.in
}

publishing {
    
    publications {
        simulatorShadowJar(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/adripc64/martian-robots"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}